unit uIniUtils;

interface

uses
  System.SysUtils, System.IniFiles, Vcl.Dialogs, System.Classes;

{------------------------------------------------------------
  MÓDULO: uIniUtils
  ------------------------------------------------------------
  Este módulo se encarga de gestionar todo lo relacionado con el
  archivo de configuración "Gevensoft.ini".

  Contiene funciones reutilizables que permiten:
    - Leer y guardar los datos de conexión a la base de datos.
    - Manipular secciones y claves de archivos INI genéricos.
    - Crear automáticamente el archivo INI si no existe.

  De esta manera, cualquier formulario puede acceder o modificar
  la configuración sin repetir código.
-------------------------------------------------------------}

{------------------------------------------------------------
  FUNCIONES PRINCIPALES DE CONEXIÓN
-------------------------------------------------------------}
procedure LeerDatosConexion(var Servidor, BaseDatos, Usuario, Clave: string; var Puerto: Integer);
procedure GuardarDatosConexion(Servidor, BaseDatos, Usuario, Clave: string; Puerto: Integer);
function LeerPrimeraEjecucion: Integer;
procedure GuardarPrimeraEjecucion(Valor: Integer);

{------------------------------------------------------------
  FUNCIONES GENÉRICAS PARA CUALQUIER ARCHIVO INI
-------------------------------------------------------------}
function RutaIni: string;
procedure VerificarOCrearIni;

implementation

{------------------------------------------------------------
  FUNCIÓN: RutaIni
  ------------------------------------------------------------
  Devuelve la ruta completa donde se encuentra el archivo
  de configuración "Gevensoft.ini", en el mismo directorio
  donde se ejecuta la aplicación.

  Con IncludeTrailingPathDelimiter se añade una \ al final del
  directorio para así evitar problemas con la Ruta. No es obligatorio
  pero si recomendable.
-------------------------------------------------------------}
function RutaIni: string;
begin
  Result := IncludeTrailingPathDelimiter(ExtractFilePath(ParamStr(0))) + 'Gevensoft.ini';
end;

{------------------------------------------------------------
  PROCEDIMIENTO: LeerPrimeraEjeccion
  ------------------------------------------------------------
  -
-------------------------------------------------------------}
function LeerPrimeraEjecucion: Integer;
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(RutaIni);
  try
    Result := Ini.ReadInteger('SYSTEM', 'PrimeraEjecucion', 0); // por defecto 0
  finally
    Ini.Free;
  end;
end;

{------------------------------------------------------------
  PROCEDIMIENTO: LeerDatosConexion
  ------------------------------------------------------------
  - Lee los valores de conexión (servidor, base de datos,
    usuario, contraseña y puerto) desde el archivo INI.
  - Si el archivo no existe, se muestra un mensaje de error.
-------------------------------------------------------------}
procedure LeerDatosConexion(var Servidor, BaseDatos, Usuario, Clave: string; var Puerto: Integer);
var
  Ini: TIniFile;
  Ruta: string;
begin
  Ruta := RutaIni;
  if not FileExists(Ruta) then
  begin
    ShowMessage('No se encontró el archivo Gevensoft.ini');
    Exit;
  end;

  Ini := TIniFile.Create(Ruta);
  try
    Servidor  := Ini.ReadString('DATABASE', 'Server', 'localhost');
    BaseDatos := Ini.ReadString('DATABASE', 'Database', 'bdgevensoftbase');
    Usuario   := Ini.ReadString('DATABASE', 'User', 'postgres');
    Clave     := Ini.ReadString('DATABASE', 'Password', '');
    Puerto    := Ini.ReadInteger('DATABASE', 'Port', 5432);
  finally
    Ini.Free;
  end;
end;

{------------------------------------------------------------
  PROCEDIMIENTO: GuardarDatosConexion
  ------------------------------------------------------------
  - Guarda los datos de conexión proporcionados por el usuario
    dentro de la sección [DATABASE] del archivo INI.
  - Si el archivo no existe, lo crea automáticamente.
-------------------------------------------------------------}
procedure GuardarDatosConexion(Servidor, BaseDatos, Usuario, Clave: string; Puerto: Integer);
var
  Ini: TIniFile;
  Ruta: string;
begin
  Ruta := RutaIni;

  Ini := TIniFile.Create(Ruta);
  try
    Ini.WriteString('DATABASE', 'Server', Servidor);
    Ini.WriteString('DATABASE', 'Database', BaseDatos);
    Ini.WriteString('DATABASE', 'User', Usuario);
    Ini.WriteString('DATABASE', 'Password', Clave);
    Ini.WriteInteger('DATABASE', 'Port', Puerto);
  finally
    Ini.Free;
  end;

  ShowMessage('Datos guardados correctamente.');
end;

{------------------------------------------------------------
  PROCEDIMIENTO: VerificarOCrearIni
  ------------------------------------------------------------
  - Comprueba si el archivo "Gevensoft.ini" existe.
  - Si no existe, lo crea automáticamente con valores por defecto
    para la conexión a la base de datos principal.
  - Esta función se llama al iniciar la aplicación (en el DPR),
    asegurando que el archivo siempre esté disponible.
-------------------------------------------------------------}
procedure VerificarOCrearIni;
var
  Ruta: string;
  Ini: TIniFile;
begin
  Ruta := RutaIni;

  // Si no existe el archivo, lo creamos con valores por defecto
  if not FileExists(Ruta) then
  begin
    Ini := TIniFile.Create(Ruta);
    try
      Ini.WriteString('DATABASE', 'Server', 'localhost');
      Ini.WriteString('DATABASE', 'Database', 'bdgevensoftbase');
      Ini.WriteString('DATABASE', 'User', 'postgres');
      Ini.WriteString('DATABASE', 'Password', '2003');
      Ini.WriteInteger('DATABASE', 'Port', 5432);
    finally
      Ini.Free;
    end;
    ShowMessage('Archivo Gevensoft.ini creado automáticamente en: ' + Ruta);
  end;
end;

{------------------------------------------------------------
  PROCEDIMIENTO: LeerPrimeraEjeccion
  ------------------------------------------------------------
  -
-------------------------------------------------------------}
procedure GuardarPrimeraEjecucion(Valor: Integer);
var
  Ini: TIniFile;
begin
  Ini := TIniFile.Create(RutaIni);
  try
    Ini.WriteInteger('SYSTEM', 'PrimeraEjecucion', Valor);
  finally
    Ini.Free;
  end;
end;


end.

